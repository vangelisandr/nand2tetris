// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Not16(in=x, out=Notx);
    Not16(in=y, out=Noty);
    
    Add16(a=x, b=Notx, out=Invx);
    Add16(a=y, b=Noty, out=Invy);

    Inc16(in=Invx, out=Zx);
    Inc16(in=Invy, out=Zy);

    Mux16(a=x, b=Zx, sel=zx, out=zxout);
    Mux16(a=y, b=Zy, sel=zy, out=zyout);

    Not16(in=zxout, out=Notzxout);
    Not16(in=zyout, out=Notzyout);

    Mux16(a=zxout, b=Notzxout, sel=nx, out=nxout);
    Mux16(a=zyout, b=Notzyout, sel=ny, out=nyout);

    Add16(a=nxout, b=nyout, out=preFadd);
    And16(a=nxout, b=nyout, out=preFand);

    Mux16(a=preFand, b=preFadd, sel=f, out=fout);

    Not16(in=fout, out=Notfout);

    Mux16(a=fout, b=Notfout, sel=no, out=out, out[0..7]=zrlower, out[8..15]=zrhigher, out[15]=ng);

    Or8Way(in=zrlower, out=zrlowerOR);
    Or8Way(in=zrhigher, out=zrhigherOR);
    Or(a=zrhigherOR, b=zrlowerOR, out=notzr);
    Not(in=notzr, out=zr);
}
